#+ summary: Select, among SIRIUS anotations of Tabernaemontana coffeoides seeds extract, the ones that are reported in the Tabernaemontana genus.
#+ description: Select, among SIRIUS anotations of Tabernaemontana coffeoides seeds extract, the ones that are reported in the Tabernaemontana genus.
#+ endpoint: https://enpkg.commons-lab.org/graphdb/repositories/ENPKG
#+ tags:
#+   - ENPKG


PREFIX enpkg: <https://enpkg.commons-lab.org/kg/>
PREFIX enpkgmodule: <https://enpkg.commons-lab.org/module/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX pr: <http://www.wikidata.org/prop/reference/>

SELECT DISTINCT ?ik2d ?genus
WHERE
    {  
    ?material enpkg:has_lab_process ?extract .
    ?material enpkgmodule:has_organe ?organe ;
            enpkg:has_wd_id ?wd_sp .
    FILTER(regex(str(?extract), "VGF152_B02")) # Tabernaemontana coffeoides seeds extract
    ?extract enpkg:has_LCMS ?lcms .
        ?lcms rdf:type enpkg:LCMSAnalysisPos .
        ?lcms enpkg:has_lcms_feature_list ?feature_list .
            ?feature_list enpkg:has_lcms_feature ?feature .
                ?feature enpkg:has_sirius_annotation ?annotation . 
                    ?annotation enpkg:has_InChIkey2D ?ik2d .
                        ?ik2d enpkg:has_smiles ?smiles .
                        ?ik2d enpkg:is_InChIkey2D_of ?ik .
                            ?ik enpkg:has_wd_id ?wd_id .
        {
        SELECT DISTINCT ?wd_id ?genus WHERE {
            ?material enpkg:has_lab_process ?extract ;
                enpkg:has_wd_id ?wd_sp .
                FILTER(regex(str(?extract), "VGF152_B02")) .
                OPTIONAL{
                    service <https://query.wikidata.org/sparql> {
                        ?wd_sp wdt:P225 ?species_name .
                        ?genus wdt:P31 wd:Q16521 ;
                                wdt:P105 wd:Q34740 ;
                                ^wdt:P171* ?wd_sp .
                        ?childtaxa wdt:P171* ?genus .
                        ?wd_id wdt:P703 ?childtaxa
                    }
                }
            }
        }
    }