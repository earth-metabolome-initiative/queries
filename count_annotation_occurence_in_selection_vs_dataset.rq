#+ summary: Count the occurence of structural annotations (SIRIUS with cut-off score) in a selection of samples and in the complete dataset.
#+ description: Count the occurence of structural annotations (SIRIUS with cut-off score) in a selection of samples and in the complete dataset.
#+ endpoint: https://enpkg.commons-lab.org/graphdb/repositories/ENPKG
#+ tags:
#+   - ENPKG



PREFIX enpkg: <https://enpkg.commons-lab.org/kg/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT DISTINCT ?ik_2d ?smiles ?np_class ?np_superclass ?np_pathway ?count ?count_in_group
WHERE
    {
    ?ik_2d rdf:type ?InChIkey2D . 
    ?ik_2d enpkg:has_smiles ?smiles . 
    ?ik_2d enpkg:has_npc_class ?np_class .
    ?ik_2d enpkg:has_npc_superclass ?np_superclass .
    ?ik_2d enpkg:has_npc_pathway ?np_pathway .
        ?annotation enpkg:has_InChIkey2D ?ik_2d . 
            ?feature enpkg:has_sirius_annotation|enpkg:has_isdb_annotation ?annotation .
            ?feature_list enpkg:has_lcms_feature ?feature .
                ?lcms enpkg:has_lcms_feature_list ?feature_list .
                ?sample enpkg:has_LCMS ?lcms .
                FILTER(regex(str(?sample), "VGF150_H08|VGF146_F09|VGF146_E08|VGF150_A09|VGF155_D05|VGF139_H07")) #cluster of active extracts
    {
        SELECT DISTINCT ?ik_2d (COUNT(DISTINCT(?sample)) AS ?count_in_group)
        WHERE {   
        ?annotation rdf:type enpkg:SiriusStructureAnnotation .
            ?annotation enpkg:has_cosmic_score ?cosmic .
            FILTER(?cosmic >= 0.4)
            ?annotation enpkg:has_InChIkey2D ?ik_2d . 
            ?feature enpkg:has_sirius_annotation|enpkg:has_isdb_annotation ?annotation .
            ?feature_list enpkg:has_lcms_feature ?feature .
                ?lcms enpkg:has_lcms_feature_list ?feature_list .
                    ?sample enpkg:has_LCMS ?lcms .
                    FILTER(regex(str(?sample), "VGF150_H08|VGF146_F09|VGF146_E08|VGF150_A09|VGF155_D05|VGF139_H07")) #cluster of active extracts
        } GROUP BY ?ik_2d ORDER BY DESC(?count_in_group)
    }
        {
        SELECT DISTINCT ?ik_2d (COUNT(DISTINCT(?sample)) AS ?count)
        WHERE {   
            ?annotation rdf:type enpkg:SiriusStructureAnnotation .
                ?annotation enpkg:has_cosmic_score ?cosmic .
                FILTER(?cosmic >= 0.4)
                ?annotation enpkg:has_InChIkey2D ?ik_2d . 
                ?feature enpkg:has_sirius_annotation|enpkg:has_isdb_annotation ?annotation .
                    ?feature_list enpkg:has_lcms_feature ?feature .
                        ?lcms enpkg:has_lcms_feature_list ?feature_list .
                        ?sample enpkg:has_LCMS ?lcms .
        } GROUP BY ?ik_2d  
        }
    }
