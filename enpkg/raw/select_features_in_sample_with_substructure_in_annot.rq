#+ summary: Among the structural annotations in Tabernaemontana coffeoides seeds extract, get the ones that contain an Aspidospermidine scaffold scaffold.
#+ description: Among the structural annotations in Tabernaemontana coffeoides seeds extract, get the ones that contain an Aspidospermidine scaffold scaffold.
#+ endpoint: https://enpkg.commons-lab.org/graphdb/repositories/ENPKG
#+ tags:
#+   - ENPKG


PREFIX enpkg: <https://enpkg.commons-lab.org/kg/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX idsm: <https://idsm.elixir-czech.cz/sparql/endpoint/>
PREFIX sachem: <http://bioinfo.uochb.cas.cz/rdf/v1.0/sachem#>

SELECT DISTINCT ?ik2d ?smiles
WHERE
    { 
    ?extract rdf:type enpkg:LabExtract .
    FILTER(regex(str(?extract), "VGF152_B02")) # Tabernaemontana coffeoides seeds extract
        ?extract enpkg:has_LCMS ?lcms .
            ?lcms enpkg:has_lcms_feature_list ?feature_list .
            ?feature_list enpkg:has_lcms_feature ?feature .
                ?feature enpkg:has_sirius_annotation|enpkg:has_isdb_annotation ?annotation . 
                ?annotation enpkg:has_InChIkey2D ?ik2d .
                    ?ik2d enpkg:has_smiles ?smiles .
                    ?ik2d enpkg:is_InChIkey2D_of ?ik .
                        ?ik enpkg:has_wd_id ?wd_id .
                        SERVICE idsm:wikidata {
                            VALUES ?SUBSTRUCTURE {
                            "CCC12CCCN3C1C4(CC3)C(CC2)NC5=CC=CC=C45" # Aspidospermidine scaffold
                            }
                            ?wd_id sachem:substructureSearch _:b16.
                            _:b16 sachem:query ?SUBSTRUCTURE.
                        }      
    }