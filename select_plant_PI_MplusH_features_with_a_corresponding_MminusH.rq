#+ summary: Among Melochia umbellata LCMS features in PI mode, get the ones that are annoatted as [M+H]+ by SIRIUS and for which a LCMS feature in NI mode with the corresponding [M-H]- m/z is found.
#+ description: Among Melochia umbellata LCMS features in PI mode, get the ones that are annoatted as [M+H]+ by SIRIUS and for which a LCMS feature in NI mode with the corresponding [M-H]- m/z is found.
#+ endpoint: https://enpkg.commons-lab.org/graphdb/repositories/ENPKG
#+ tags:
#+   - ENPKG
#+   - Templated
#+ defaults:
#+   - ppmTolerance: 5
#+   - rtTolerance: 0.05
#+   - TaxonName: Melochia umbellata



PREFIX enpkg: <https://enpkg.commons-lab.org/kg/>
PREFIX enpkgmodule: <https://enpkg.commons-lab.org/module/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT ?feature ?rt ?pm ?feature_opp ?rt_opp ?pm_opp
WHERE
{
    {
        ?source_sample rdf:type enpkg:RawMaterial ;
                    enpkg:submitted_taxon ?_TaxonName ;
                    enpkg:has_lab_process ?sample.
        
        ?sample rdf:type enpkg:LabExtract.
        
        ?sample enpkg:has_LCMS ?lcms .
            ?lcms rdf:type enpkg:LCMSAnalysisPos .
            ?lcms enpkg:has_lcms_feature_list ?feature_list .
                ?feature_list enpkg:has_lcms_feature ?feature .
                    ?feature enpkg:has_parent_mass ?pm .
                    ?feature enpkg:has_retention_time ?rt .
                    ?feature enpkg:has_sirius_annotation ?sirius .
                        ?sirius enpkg:has_sirius_adduct ?adduct .
                        FILTER(regex(str(?adduct), "[M+H]+"))
            
        ?sample enpkg:has_LCMS ?lcms_opp .
        ?lcms_opp rdf:type enpkg:LCMSAnalysisNeg .
        ?lcms_opp enpkg:has_lcms_feature_list ?feature_list_opp .
            ?feature_list_opp enpkg:has_lcms_feature ?feature_opp .
            ?feature_opp enpkg:has_parent_mass ?pm_opp .
            ?feature_opp enpkg:has_retention_time ?rt_opp .
        FILTER(((?rt - ?_rtTolerance_integer) < ?rt_opp) && ((?rt + ?_rtTolerance_integer) > ?rt_opp))
        FILTER((?pm_opp > ((?pm - 2.014) - ((?_ppmTolerance_integer * 0.000001) * (?pm - 2.014)))) && (?pm_opp < ((?pm - 2.014) + ((?_ppmTolerance_integer * 0.000001) * (?pm - 2.014)))))
    }
}


