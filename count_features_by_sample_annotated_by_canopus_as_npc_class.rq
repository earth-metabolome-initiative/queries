#+ summary: Count the number of features annotated as aspidosperma type alkaloids by CANOPUS (p>0.5) in the different samples
#+ description: Count the number of features annotated as aspidosperma type alkaloids by CANOPUS (p>0.5) in the different samples
#+ endpoint: https://enpkg.commons-lab.org/graphdb/repositories/ENPKG
#+ tags:
#+   - ENPKG
#+   - Templated
#+ enumerate:
#+   - NPCClass:
#+     - Aspidosperma_type

PREFIX enpkg: <https://enpkg.commons-lab.org/kg/>
PREFIX enpkgmodule: <https://enpkg.commons-lab.org/module/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>

SELECT ?extract ?organe ?species_name ?genus_name ?family_name ?count_of_selected_class
WHERE
    {  
    ?material enpkg:has_lab_process ?extract .
        ?material enpkgmodule:has_organe ?organe .
        ?material enpkg:has_wd_id ?wd_sp .
        OPTIONAL
        {
            SERVICE <https://query.wikidata.org/sparql> {
            ?wd_sp wdt:P225 ?species_name .
            ?family wdt:P31 wd:Q16521 ;
                wdt:P105 wd:Q35409 ;
                wdt:P225 ?family_name ;
                ^wdt:P171* ?wd_sp .
            ?genus wdt:P31 wd:Q16521 ;
                wdt:P105 wd:Q34740 ;
                wdt:P225 ?genus_name ;
                ^wdt:P171* ?wd_sp 
            }
        }
        {
            SELECT ?extract (COUNT(DISTINCT ?feature) AS ?count_of_selected_class)
            WHERE
            {   
                ?extract rdf:type enpkg:LabExtract .
                    ?extract enpkg:has_LCMS ?lcms .
                        ?lcms rdf:type enpkg:LCMSAnalysisPos .
                        ?lcms enpkg:has_lcms_feature_list ?feature_list .
                            ?feature_list enpkg:has_lcms_feature ?feature .
                                ?feature enpkg:has_canopus_annotation ?canopus .
                                    ?canopus enpkg:has_canopus_npc_class ?np_class .
                                    ?canopus enpkg:has_canopus_npc_class_prob ?class_prob .
                                    FILTER(regex(str(?np_class), ?_NPCClass_iri)) .
                                    FILTER((?class_prob > 0.5)) .
            } GROUP BY ?extract ORDER BY DESC(?count_of_selected_class)
        }
    }
