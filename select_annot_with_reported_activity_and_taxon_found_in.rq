# Among structural annotations in Melochia umbellata extract,
# select the ones that have an activity reported against T. cruzi (ChEMBL)
# with the taxon they are already reported in (Wikidata).

PREFIX enpkg: <https://enpkg.commons-lab.org/kg/>
PREFIX enpkgmodule: <https://enpkg.commons-lab.org/module/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>

SELECT DISTINCT ?ik ?chemblid ?name ?type ?value ?wd_id ?taxon ?taxon_name
WHERE
    {
    service <https://query.wikidata.org/sparql> {
        ?wd_id wdt:P31 wd:Q113145171 .
            ?wd_id wdt:P703 ?taxon .
            ?taxon wdt:P225 ?taxon_name .
        }   
        { SELECT DISTINCT ?ik ?chemblid ?name ?type ?value ?wd_id
            WHERE
            { 
                ?sample rdf:type enpkg:LabExtract
                FILTER(regex(str(?sample), "VGF156_A06")) # Melochia umbellata extract 
                ?sample enpkg:has_LCMS ?lcms .
                    ?lcms rdf:type enpkg:LCMSAnalysisPos .
                    ?lcms enpkg:has_lcms_feature_list ?feature_list .
                        ?feature_list enpkg:has_lcms_feature ?feature .
                            {?feature enpkg:has_sirius_annotation ?annotation .
                                ?annotation enpkg:has_InChIkey2D ?ik2d .
                                ?annotation enpkg:has_cosmic_score ?cosmic .
                                ?annotation enpkg:has_zodiac_score ?zodiac .
                                FILTER((?cosmic > 0.5) && (?zodiac > 0.8))}
                            UNION
                            {?feature enpkg:has_isdb_annotation ?annotation .
                                ?annotation enpkg:has_taxo_score ?taxo .
                                FILTER(?taxo >= 6)}

                ?annotation enpkg:has_InChIkey2D ?ik2d .
                    ?ik2d enpkg:has_smiles ?smiles .
                    ?ik2d enpkg:is_InChIkey2D_of ?ik .
                        ?ik enpkgmodule:has_chembl_id ?chemblid .
                        ?ik enpkg:has_wd_id ?wd_id .
                            ?chemblid enpkgmodule:has_chembl_activity ?chembl_activity .
                            ?chembl_activity enpkgmodule:target_name ?name .
                            FILTER(regex(str(?name), "Trypanosoma cruzi")) 
                            ?chembl_activity enpkgmodule:activity_type ?type .
                            ?chembl_activity enpkgmodule:activity_value ?value .
            }
        }      
    }