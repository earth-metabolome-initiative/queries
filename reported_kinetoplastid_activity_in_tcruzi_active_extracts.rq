#+ summary: Among structural annotations (PI mode) in T. cruzi clustered active extracts, get annotations with reported activity againt T. cruzi, T. brucei or L.donovani
#+ description: Among structural annotations (PI mode) in T. cruzi clustered active extracts, get annotations with reported activity againt T. cruzi, T. brucei or L.donovani
#+ endpoint: https://enpkg.commons-lab.org/graphdb/repositories/ENPKG
#+ tags:
#+   - ENPKG

PREFIX enpkg: <https://enpkg.commons-lab.org/kg/>
PREFIX enpkgmodule: <https://enpkg.commons-lab.org/module/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT DISTINCT ?ik ?chemblid ?sample ?name ?type ?value ?wd_id ?npclass
WHERE { 
    ?sample rdf:type enpkg:LabExtract
    FILTER(regex(str(?sample), "VGF150_H08|VGF146_F09|VGF146_E08|VGF150_A09|VGF155_D05|VGF139_H07")) # active extracts
    ?sample enpkg:has_LCMS ?lcms .
        ?lcms rdf:type enpkg:LCMSAnalysisPos ;
            enpkg:has_lcms_feature_list ?feature_list .
            ?feature_list enpkg:has_lcms_feature ?feature .
                {?feature enpkg:has_sirius_annotation ?annotation .
                    ?annotation enpkg:has_InChIkey2D ?ik2d .
                    ?annotation enpkg:has_cosmic_score ?cosmic .
                    ?annotation enpkg:has_zodiac_score ?zodiac .
                    FILTER((?cosmic > 0.4) && (?zodiac > 0.5))}
                UNION
                {?feature enpkg:has_isdb_annotation ?annotation .
                    ?annotation enpkg:has_taxo_score ?taxo .
                    FILTER(?taxo >= 6)}

                ?annotation enpkg:has_InChIkey2D ?ik2d .
                    ?ik2d enpkg:is_InChIkey2D_of ?ik .
                        ?ik enpkgmodule:has_chembl_id ?chemblid ;
                            enpkg:has_npc_class ?npclass ;
                            enpkg:has_wd_id ?wd_id .
                            ?chemblid enpkgmodule:has_chembl_activity ?chembl_activity .
                                ?chembl_activity enpkgmodule:target_name ?name ;
                                    enpkgmodule:activity_type ?type ;
                                    enpkgmodule:activity_value ?value .
}
